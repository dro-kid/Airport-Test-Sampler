*** Settings ***

Documentation  A resource file containing the ParkCalc app specific keywords and variables for data-driven tests that create our own domain specific language. Also SeleniumLibrary itself is imported here so that tests only need to import this resource file.

Library        SeleniumLibrary


*** Variables ***

${BROWSER}       firefox
${DELAY}         0
${PARKCALCPAGE}  http://www.shino.de/parkcalc/
${COST_ITEM}     xpath=//tr[td/div[@class='SubHead'] = 'estimated Parking costs']/td/span/b
${PAGE_TITLE}    Parking Cost Calculator

*** Keywords ***

Open Park Calc Page
    Open Browser  ${PARKCALCPAGE}  ${BROWSER}
    Set Selenium Speed  ${DELAY}
    Title Should Be  ${PAGE_TITLE}

Park Calc  [Arguments]  ${parkingLot}  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Title Should Be  ${PAGE_TITLE}
    Select From List  ParkingLot  ${parkingLot}
    Input Text  StartingTime  ${startingTime}
    Select Radio Button  StartingTimeAMPM  ${startingAmPm}
    Input Text  StartingDate  ${startingDate}
    Input Text  LeavingTime  ${leavingTime}
    Select Radio Button  LeavingTimeAMPM  ${leavingAmPm}
    Input Text  LeavingDate  ${leavingDate}
    Click Button  Submit
    Calculated Cost Should Be  ${expectedPrice}


Calculated Cost Should Be  [Arguments]  ${expectedCost}
    ${actualCost} =  Get Text  ${COST_ITEM}
    Log  Actual costs: ${actualCost}
    Page Should Contain  ${expectedCost}


Valet Parking  [Arguments]  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Park Calc   Valet Parking  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}

Short-Term Parking  [Arguments]  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Park Calc   Short-Term Parking  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}

Long-Term Garage Parking  [Arguments]  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Park Calc   Long-Term Garage Parking  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}

Long-Term Surface Parking  [Arguments]  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Park Calc   Long-Term Surface Parking  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}

Economy Parking  [Arguments]  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
    Park Calc   Economy Parking  ${startingDate}  ${startingTime}  ${startingAmPm}  ${leavingDate}  ${leavingTime}  ${leavingAmPm}  ${expectedPrice}
